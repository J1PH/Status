apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def isRunningOnTravis = System.getenv("CI") == "true"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.james.status"
        minSdkVersion 16
        targetSdkVersion 28
        maxSdkVersion 25
        versionCode 42
        versionName "3.8-beta6"
        vectorDrawables.useSupportLibrary = true
        resConfigs "ar", "en", "es", "ko", "zh"

        if (isRunningOnTravis) {
            def githubToken = System.getenv("GITHUB_TOKEN_PUB")
            buildConfigField "String", "GITHUB_TOKEN", "\"$githubToken\""
        } else {
            buildConfigField "String", "GITHUB_TOKEN", 'null'
        }
    }

    sourceSets {
        main {
            res {
                srcDirs = [
                        'src/main/res-google',
                        'src/main/res-icons8',
                        'src/main/res-materialdesignicons',
                        'src/main/res-status',
                        'src/main/res'
                ]
            }
            assets {
                srcDirs = [
                        'src/main/assets/audiowide/assets',
                        'src/main/assets/blackopsone/assets',
                        'src/main/assets/hennypenny/assets',
                        'src/main/assets/iceland/assets',
                        'src/main/assets/megrim/assets',
                        'src/main/assets/monoton/assets',
                        'src/main/assets/newrocker/assets',
                        'src/main/assets/nosifer/assets',
                        'src/main/assets/permanentmarker/assets',
                        'src/main/assets/playball/assets',
                        'src/main/assets/roboto/assets',
                        'src/main/assets/vt323/assets',
                        'src/main/assets/wallpoet/assets'
                ]

                filter.exclude 'LICENSE'
            }
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    if (isRunningOnTravis) {
        buildTypes.release.signingConfig = signingConfigs.release
        signingConfigs.release.storeFile = file("../key.jks")
        signingConfigs.release.storePassword = System.getenv("KEYSTORE_PASSWORD")
        signingConfigs.release.keyAlias = System.getenv("KEYSTORE_ALIAS")
        signingConfigs.release.keyPassword = System.getenv("KEYSTORE_ALIAS_PASSWORD")
    }

    /**
     * This is used to circumvent some of Google Play's restrictions
     * and allow assets under a different license to exist only in
     * the Google Play apks without interfering with the OSS version.
     */
    flavorDimensions "distribution"
    productFlavors {
        gplay {
            dimension "distribution"
        }

        oss {
            dimension "distribution"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.drozdzynski:Steppers:90411a5'
    implementation 'james.signalstrengths:signalstrengths:0.0.4'
    implementation 'com.afollestad:async:0.2.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'me.jfenn:Attribouter:0.1.5'
    implementation 'me.jfenn.ColorPickerDialog:base:0.2.1'
    implementation 'me.jfenn.ColorPickerDialog:imagepicker:0.2.1'
    implementation 'me.jfenn:AndroidUtils:0.0.1'
}
